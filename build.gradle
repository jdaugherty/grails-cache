buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath platform("org.grails:grails-bom:${grailsVersion}")
        classpath "org.grails:grails-gradle-plugin"
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        if (System.getenv("GITHUB_MAVEN_PASSWORD") && !grailsVersion.endsWith('-SNAPSHOT')) {
            System.out.println("Adding Grails Core Repo")
            maven {
                url = 'https://maven.pkg.github.com/grails/grails-core'
                credentials {
                    username = 'DOES_NOT_MATTER'
                    password = System.getenv("GITHUB_MAVEN_PASSWORD")
                }
            }
        }
    }
}

group = "org.grails.plugins"
version = project.projectVersion

subprojects {
    if(project.name == 'plugin') {
        version = projectVersion
        group = "org.grails.plugins"

        apply plugin:"org.grails.grails-publish"
        grailsPublish {
            artifactId = 'cache'
            githubSlug = "grails/grails-cache"
            license {
                name = 'Apache-2.0'
            }
            title = "Grails Cache Plugin"
            desc = "Provides AST transformations for caching method calls"
            developers = [jeffbrown: "Jeff Scott Brown", "puneetbehl": "Puneet Behl"]
        }
    }
}
